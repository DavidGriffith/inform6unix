<HTML><HEAD><TITLE>Answer to Exercise 31</TITLE></HEAD>
<BODY BGCOLOR="#FFFFFF">
<TABLE><TR><TD Valign="top"><IMG SRC="../icons/exercise.gif" ALT="??"><TD bgcolor="#FAA89B"><B>ANSWER TO EXERCISE 31</B><TR><TD><TD>
<P>


Martha and the sealed room are defined as follows:
<PRE>
Object sealed_room "Sealed Room"
  with description
          "I'm in a sealed room, like a squash court without a door,
           maybe six or seven yards across",
  has  light;
Object -&#62; ball "red ball" with name "red" "ball";
Object -&#62; martha "Martha"
  with name "martha",
       orders
       [ r; r=parent(self);
           Give:
               if (noun notin r) "~That's beyond my telekinesis.~";
               if (noun==self) "~Teleportation's too hard for me.~";
               move noun to player;
               "~Here goes...~ and Martha's telekinetic talents
                   magically bring ", (the) noun, " to your hands.";
           Look:
               print "~", (string) r.description;
               if (children(r)==1) ".  There's nothing here but me.~";
               print ".  I can see ";
               WriteListFrom(child(r),CONCEAL_BIT+ENGLISH_BIT);
              ".~";
           default: "~Afraid I can't help you there.~";
       ],
       life
       [;  Ask: "~You're on your own this time.~";
           Tell: "Martha clucks sympathetically.";
           Answer: "~I'll be darned,~ Martha replies.";
       ],
   has animate female concealed proper;
</PRE>

but the really interesting part is the <TT>InScope</TT> routine to fix
things up:
<PRE>
[ InScope actor;
   if (actor==martha) PlaceInScope(player);
   if (actor==player &#38;&#38; scope_reason==TALKING_REASON)
       PlaceInScope(martha);
   rfalse;
];
</PRE>

Note that since we want two-way communication, the player has to
be in scope to Martha too: otherwise Martha won't be able to
follow the command "martha, give me the fish'', because "me''
will refer to something beyond her scope.
</TABLE>
<HR>Back to <A HREF="../section16.html#ex31">the exercise in section 16</A><HR>
<SMALL><I>Mechanically translated to HTML from third edition as revised 16 May 1997. Copyright &#169; Graham Nelson 1993, 1994, 1995, 1996, 1997: all rights reserved.</I></SMALL></BODY></HTML>
