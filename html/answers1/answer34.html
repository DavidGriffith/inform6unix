<HTML><HEAD><TITLE>Answer to Exercise 34</TITLE></HEAD>
<BODY BGCOLOR="#FFFFFF">
<TABLE><TR><TD Valign="top"><IMG SRC="../icons/exercise.gif" ALT="??"><TD bgcolor="#FAA89B"><B>ANSWER TO EXERCISE 34</B><TR><TD><TD>
<P>

This is a crude implementation, for brevity (the real Zork
thief has an enormous stock of attached messages).  A <TT>life</TT> routine
is omitted, and of course this particular thief steals
nothing.  See 'The Thief' for a much fuller,
annotated implementation.
<PRE>
Object -&#62; thief "thief"
  with name "thief" "gentleman" "mahu" "modo",
       each_turn "^The thief growls menacingly.",
       daemon
       [ i p j n k;
           if (random(3)~=1) rfalse;
           p=parent(thief);
           objectloop (i in compass)
           {   j=p.(i.door_dir);
               if (j ofclass Object &#38;&#38; j hasnt door) n++;
           }
           if (n==0) rfalse;
           k=random(n); n=0;
           objectloop (i in compass)
           {   j=p.(i.door_dir);
               if (j ofclass Object &#38;&#38; j hasnt door) n++;
               if (n==k)
               {   move self to j;
                   if (p==location) "^The thief stalks away!";
                   if (j==location) "^The thief stalks in!";
                   rfalse;
               }
           }
       ],
  has  animate;
</PRE>

(Not forgetting to <TT>StartDaemon(thief)</TT> at some point, for instance
in the game's <TT>Initialise</TT> routine.)  So the thief walks at random but
never via doors, bridges and the like
(because these may be locked or have rules attached);
it's only a first approximation, and in a good game one should
occasionally see the thief do something surprising, such as open a
secret door.  As for the <TT>name</TT>, note that 'The Prince of darkness is
a gentleman. Modo he's called, and Mahu'
(William Shakespeare, <I> King Lear</I> III iv).
</TABLE>
<HR>Back to <A HREF="../section18.html#ex34">the exercise in section 18</A><HR>
<SMALL><I>Mechanically translated to HTML from third edition as revised 16 May 1997. Copyright &#169; Graham Nelson 1993, 1994, 1995, 1996, 1997: all rights reserved.</I></SMALL></BODY></HTML>
