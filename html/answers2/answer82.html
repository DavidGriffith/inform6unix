<HTML><HEAD><TITLE>Answer to Exercise 82</TITLE></HEAD>
<BODY BGCOLOR="#FFFFFF">
<TABLE><TR><TD Valign="top"><IMG SRC="../icons/exercise.gif" ALT="??"><TD bgcolor="#FAA89B"><B>ANSWER TO EXERCISE 82</B><TR><TD><TD>
<P>


Note the sneaky way looking through the window is implemented, and that
the 'on the other side' part of the room description isn't printed
in that case.
<PRE>
Property far_side;
Class  Window_Room
  with description
          "This is one end of a long east/west room.",
       before
       [;  Examine, Search: ;
           default:
             if (inp1~=1 &#38;&#38; noun~=0 &#38;&#38; noun in self.far_side)
                 print_ret (The) noun, " is on the far side of
                    the glass.";
             if (inp2~=1 &#38;&#38; second~=0 &#38;&#38; second in self.far_side)
                 print_ret (The) second, " is on the far side of
                    the glass.";
       ],
       after
       [;  Look:
             if (ggw has general) rfalse;
             print "^The room is divided by a great glass window";
             if (location.far_side hasnt light) " onto darkness.";
             print ", stretching from floor to ceiling.^";
             if (Locale(location.far_side,
                    "Beyond the glass you can see",
                    "Beyond the glass you can also see")~=0) ".";
       ],
  has  light;
Window_Room window_w "West of Window"
  with far_side window_e;
Window_Room window_e "East of Window"
  with far_side window_w;
Object ggw "great glass window"
  with name "great" "glass" "window",
       before
       [ place; Examine, Search: place=location;
               if (place.far_side hasnt light)
                   "The other side is dark.";
               give self general;
               PlayerTo(place.far_side,1); &#60;Look&#62;; PlayerTo(place,1);
               give self ~general;
               give place.far_side ~visited; rtrue;
       ],
       found_in window_w window_e,
  has  scenery;
</PRE>

A few words about <TT>inp1</TT> and <TT>inp2</TT> are in order.  <TT>noun</TT> and
<TT>second</TT> can hold either objects or numbers, and it's sometimes useful
to know which.  <TT>inp1</TT> is equal to <TT>noun</TT> if that's an object,
or 1 if that's a number; likewise for <TT>inp2</TT> and <TT>second</TT>.  (In this
case we're just being careful that the action <TT>SetTo eggtimer 35</TT>
wouldn't be stopped if object 35 happened to be on the other side of
the glass.)  We also need:
<PRE>
[ InScope actor;
   if (actor in window_w &#38;&#38; window_e has light) ScopeWithin(window_e);
   if (actor in window_e &#38;&#38; window_w has light) ScopeWithin(window_w);
   rfalse;
];
</PRE>

</TABLE>
<HR>Back to <A HREF="../section28.html#ex82">the exercise in section 28</A><HR>
<SMALL><I>Mechanically translated to HTML from third edition as revised 16 May 1997. Copyright &#169; Graham Nelson 1993, 1994, 1995, 1996, 1997: all rights reserved.</I></SMALL></BODY></HTML>
